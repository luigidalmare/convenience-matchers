/*
 * Released under the terms of the MIT License.
 *
 * Copyright (c) 2017 objecttrouve.org <un.object.trouve@gmail.com>
 *
 */
buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.4.3"
    }
}

apply plugin: 'java-library'
apply plugin: "me.champeau.gradle.jmh"
apply plugin: 'maven'
apply plugin: 'signing'




repositories {
    mavenCentral()
}

group='org.objecttrouve'
sourceCompatibility=JavaVersion.VERSION_1_8;
archivesBaseName = "convenience-matchers"


dependencies {
    api group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
    implementation group: 'cglib', name: 'cglib', version: '3.2.5'
    implementation group: 'org.objenesis', name: 'objenesis', version: '2.6'

    testImplementation group: 'junit', name: 'junit', version: '4.12'

    jmh 'org.openjdk.jmh:jmh-core:1.19'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.19'
    jmh group: 'junit', name: 'junit', version: '4.12'

}



/* Workaround for weird duplication and "No benchmarks to run" issues. */
task removeJmhDuplicates(type: Delete) {
    delete fileTree(
            dir: "${buildDir}/classes/java/jmh/org/objecttrouve/testing/matchers/fluentatts/generated",
            exclude: "*.java"
    )
}
task fixJmhMetaInf(type: Copy){
    from "${buildDir}/classes/java/jmh/META-INF"
    into "${buildDir}/jmh-generated-classes/META-INF"
}
jmhJar.dependsOn(fixJmhMetaInf)
removeJmhDuplicates.mustRunAfter(jmhCompileGeneratedClasses)
fixJmhMetaInf.dependsOn(removeJmhDuplicates)



jmh {
    duplicateClassesStrategy = 'fail'
    failOnError = true
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

test {
    systemProperties System.getProperties()
}

